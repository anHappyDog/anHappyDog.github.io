---
title: 北航计组P8(计组实验完结篇)
author: lonelywatch
date: 2022-12-19 19:06 +0800
categories: [BUAA,计组]
tags: [计组]
---

### P8

​	P8要求我们将自己写的流水线添加一定量的外设并放到fpga上实际运行一定功能的程序。

可分为两部分：**流水线部分**，**程序软件部分**。

### 流水线

#### IM,DM

​	我们需要将代码综合成bit文件，在P8之前都是仿真，而到了P8需要将一些不可综合的代码删去，有initial,IM,DM,MUDIV(乘除法器)。

​	将IM,DM替换为可综合的IP核，将MUDIV替换为课程组提供的可综合的乘除法器。**IM**采用**Single ROM**，使用32位地址，宽度32，深度4096，**DM**采用**Single RAM**，使用32位地址，同步复位，宽度32，深度3172。IM的初始化需要coe文件，coe文件需要一定的格式：

```
memory_initialization_radix=16;
memory_initialization_vector = 
x,
x,
....
x;	
```

16表示十六进制，后面指令用逗号隔开，用冒号结尾。需要自己编写程序将主程序码和异常处理程序码连接在一起写成coe文件的格式。

---

​	由于采用IP核，读写方式从“同步写，异步读”变成了“**同步写，同步读**”，也就是说，想要读数据的话，数据会在**地址给出的下一个时钟上升沿给出**。意味着我们需要对流水线寄存器进行一定的处理（不能直接短接！！！！），避免出现相应的错误：

对于IM来说：

​	在我写的流水线中，如果直接短接，在stall了之后生成的nop指令的那条导致暂停的指令会变成它的下一条指令，也就是说nop后的指令会被“吞”掉，所以我添加了一个寄存器保存了指令，一般情况下短接即可，暂停时则接入寄存器的输出。像这样：

```verilog
module REGD(
	 input clk,
	 input clr,
	 input en,
	 input req,
    input[31:0] pc,
	 input[31:0] instr,
	 output[31:0] instrd,
	 output[31:0] pcd,
	 output[4:0] rsd,
	 output[4:0] rtd,
	 output[15:0] imm16,
	 output[25:0] imm26,
	 input[4:0] exccodef,
	 output[4:0] exccodeoldf,
	 input bdind,
	 output bdine
	 );
	reg[31:0] pcreg;
	reg[4:0] exccodereg;
	reg[31:0] instrreg;
	reg isstallreg;
	reg bdinreg;
	always@(posedge clk) begin
		if(clr | req) begin	
			exccodereg <= 5'b0;
			pcreg <= (req == 1)?32'h0000_4180:32'b0;
			bdinreg <= 0;
			instrreg <= 0;
			isstallreg <= 0;
		end
		else if(en) begin
			pcreg <= pc;
			exccodereg <= exccodef;
			bdinreg <= bdind;
			isstallreg <= 0;
		end
		else if(!en) begin
			isstallreg <= 1;
		end
		if(isstallreg == 0) begin
			instrreg <= instr;
		end
	end
	assign bdine = bdinreg;
	assign exccodeoldf = exccodereg;
	assign instrd =(exccodeoldf > 0 || clr || req)?32'b0:
						(isstallreg == 1)?instrreg :instr;
endmodule
```

​	其实就是时序性的问题，这里我用寄存器来保存是否暂停的状态，是为了处理出现暂停多个周期的情况，指令存储器存着的永远是短接的前一条指令。

---

​	对于DM，需要判断输出数据是否来自DM，若是来自其他外设，则一样需要流水寄存器来存储他们。

```verilog
	assign dmoutw = (aluoutreg > 32'h0000_2fff)?dmoutreg:dmout;
```

​	其中aluoutreg为alu的输出，在DM相关指令中则是地址。

---

**（每次修改coe文件都要重新生成相应ip核）**

	#### MUDIV

​	可以将课程组给的乘除法器封装成我们之前使用的乘除法器的模块接口，这样能最大程度地减少修改，其他没有什么要注意的。

像这样：

```verilog
module MUDIVUNIT(
 	 input clk,
	 input clr,
	 input[31:0] d1,
	 input[31:0] d2,
	 input[1:0] mudivop,
	 input in_sign,
	 input[1:0] mtop,
	 input mfop,
	 output[31:0] mudivout,
	 output busy,
	 input start
);
	wire in_ready,out_valid;
	wire[31:0] dout0,dout1;
	reg[31:0]hireg,loreg;

	assign busy = !in_ready;

	//MulDivUnit
	MulDivUnit _mudivunit(
		.clk(clk),
		.reset(clr),
		.in_src0(d1),
		.in_src1(d2),
		.in_op(mudivop),
		.in_sign(in_sign),
		.in_ready(in_ready),
		.in_valid(start),
		.out_ready(1'b1),
		.out_valid(out_valid),
		.out_res0(dout0),
		.out_res1(dout1)
	);
	always@(posedge clk) begin
		if(out_valid) begin
			hireg <= dout1;
			loreg <= dout0;
		end
		if(mtop != `MT_NOT) begin
			if(mtop == `MT_HI) begin
				hireg <= d1;
			end
			else begin
				loreg <= d1;
			end
        end
	end
	assign mudivout = (mfop == `MF_HI)?hireg:loreg;

endmodule
```

### 外设

外设总共有UART，数码管，LED，64位拨码开关，8位按键开关。

对于LED添加一组寄存器读写即可，对于开关直接读入就好，其他的需要独立写控制单元。

#### 数码管

分为数据锁存，驱动，显示三个部分。

```verilog
module DIGITALTUBEUNIT(
		input wire clk,
		input wire clr,
		input wire [31:0] digitaltube_wdata,
		input wire [3:0] digitaltube0_byteen,
		input wire [3:0] digitaltube1_byteen,
		output wire [31:0] digitaltube0_rdata,
		output wire [31:0] digitaltube1_rdata,
		output wire [7:0] digital_tube0,
		output wire [7:0] digital_tube1,
		output wire [7:0] digital_tube2,
		output wire [3:0] digital_tube_sel0,
		output wire [3:0] digital_tube_sel1,
		output wire digital_tube_sel2
    );
	reg[31:0] digitaltube0reg,digitaltube1reg;

	always @ (posedge clk) begin
		if(clr) begin
			digitaltube0reg <= 32'h0000_0000;
			digitaltube1reg <= 32'h0000_0000;
		end
		else begin
			//这里没必要判断不等于
			if(digitaltube0_byteen[0] == 1  && digitaltube0reg[7:0] != digitaltube_wdata[7:0]) begin
				digitaltube0reg[7:0]   <= digitaltube_wdata[7:0];
			end
			if(digitaltube0_byteen[1] == 1 && digitaltube0reg[15:8] != digitaltube_wdata[15:8]) begin
				digitaltube0reg[15:8]  <= digitaltube_wdata[15:8];
			end
			if(digitaltube0_byteen[2] == 1 && digitaltube0reg[23:16] != digitaltube_wdata[23:16]) begin
				digitaltube0reg[23:16] <= digitaltube_wdata[23:16];
			end
			if(digitaltube0_byteen[3] == 1 && digitaltube0reg[31:24] != digitaltube_wdata[31:24]) begin
				digitaltube0reg[31:24] <= digitaltube_wdata[31:24];
			end
			if(digitaltube1_byteen[0] == 1 && digitaltube1reg[7:0] != digitaltube_wdata[7:0]) begin
				digitaltube1reg[7:0]   <=  digitaltube_wdata[7:0];
			end

		end

	end

	function [7:0] hex2dig;   // dp = 1
        input [3:0] hex;
        begin
            case (hex)
            4'h0    : hex2dig = 8'b1000_0001;   // not G
            4'h1    : hex2dig = 8'b1100_1111;   // B, C
            4'h2    : hex2dig = 8'b1001_0010;   // not C, F
            4'h3    : hex2dig = 8'b1000_0110;   // not E, F
            4'h4    : hex2dig = 8'b1100_1100;   // not A, D, E
            4'h5    : hex2dig = 8'b1010_0100;   // not B, E
            4'h6    : hex2dig = 8'b1010_0000;   // not B
            4'h7    : hex2dig = 8'b1000_1111;   // A, B, C
            4'h8    : hex2dig = 8'b1000_0000;   // All
            4'h9    : hex2dig = 8'b1000_0100;   // not E
            4'hA    : hex2dig = 8'b1000_1000;   // not D
            4'hB    : hex2dig = 8'b1110_0000;   // not A, B
            4'hC    : hex2dig = 8'b1011_0001;   // A, D, E, F
            4'hD    : hex2dig = 8'b1100_0010;   // not A, F
            4'hE    : hex2dig = 8'b1011_0000;   // not B, C
            4'hF    : hex2dig = 8'b1011_1000;   // A, E, F, G
            default : hex2dig = 8'b1111_1111;
            endcase
        end
    endfunction

	 localparam PERIOD = 32'd25_000;
    reg [31:0] counter;
    reg [1:0] select0;

	 reg select2;
    always @(posedge clk) begin
          if (counter + 1 == PERIOD) begin
              counter <= 0;
              select0 <= select0 + 1'b1;
			  select2 <= ~select2;
			end 
         else begin
              counter <= counter + 1;
         end
    end

    assign digital_tube_sel0 = (4'b1 << select0);
	assign digital_tube_sel1 = (4'b1 << select0);
	assign digital_tube_sel2 = 1;
	
    assign digital_tube0 = hex2dig(digitaltube0reg[select0*4+: 4]);
	assign digital_tube1 = hex2dig(digitaltube0reg[(16+select0*4)+: 4]);
	//assign digital_tube2 = hex2dig(digitaltube1reg[3:0]);
	assign digital_tube2 = 8'b1111_1111;
	
    assign digitaltube0_rdata = digitaltube0reg;
	assign digitaltube1_rdata = digitaltube1reg;

endmodule


```

参照教程组的驱动代码完成，这里用到了verilog的**动态参数截取**，一般方法变量是不能使用的如[select * 4 + 4,select * 4],但是代码中这样却可以。

#### UART

UART可以接受和发送数据。

```verilog
module UARTUNIT(
	input wire clk,
	input wire clr,
	input wire uart_we,
	input wire uart_rd,
	input wire[31:0] uart_addr,	//ȥַ
	input wire[31:0] uart_wdata,
	output wire[31:0] uart_rdata,
	input wire uart_rxd,
	output wire uart_txd,
	output wire uart_int
);
	wire tx_start,tx_avai;
	wire rx_clear,rx_ready;
	wire[15:0] divr,divt;
	wire[7:0] tx_data,rx_data;
	reg[31:0] DIVR,DIVT;
	
    assign divr = DIVR[15:0];
	assign divt = DIVT[15:0];
	assign tx_data = uart_wdata[7:0];
	
    uart_tx tx (
        .clk(clk), .rstn(clr), .period(divt[15:0]), .txd(uart_txd), 
        .tx_start(tx_start), .tx_data(tx_data), .tx_avai(tx_avai)
    );
    uart_rx rx (
        .clk(clk), .rstn(clr), .period(divr[15:0]), .rxd(uart_rxd),
        .rx_clear(rx_clear), .rx_data(rx_data), .rx_ready(rx_ready)
    );
	
    assign tx_start = uart_we & (uart_addr == 32'b0);
	assign rx_clear = uart_rd & (uart_addr == 32'b0); 
	assign uart_int = rx_ready;
	
    assign uart_rdata = (uart_addr == 32'd0)?{24'b0,rx_data}:
							  (uart_addr == 32'd4)?{26'b0,tx_avai,4'b0,rx_ready}:
							  (uart_addr == 32'd8)?DIVR:
							  (uart_addr == 32'd12)?DIVT:32'b0;

    always@(posedge clk) begin
		if(clr) begin
			DIVR <= 32'd2604;
			DIVT <= 32'd2604;
		end
		else if(uart_we) begin
			case(uart_addr)
			32'd8: begin
				DIVR <= uart_wdata;
			end
			32'd12: begin
				DIVT <= uart_wdata;
			end
			default: begin
			end
			endcase
		end
	end
endmodule
```

​	其中的两个子模块为课程组给的驱动代码，我通过lw 0x7f30来得到接受数据，通过sw 0x7f30中的该地址来发送数据，通过lw 0x7f34来判断是否可发送数据(tx_avai是否为1)，是否有接受数据(rx_ready是否为1). DIVR,DIVT分别为接收和发送的除数因子。除数因子为每过多少时钟周期采样一次，即多少个时钟周期接收或发送1 bit 数据，这与波特率有关，波特率为1s接收多少位数据，进行相关计算可以得到相应波特率的除数因子。

​	这里我碰到的问题是，持续回显接收的数据时卡死，后发现应该是汇编写的哪里有问题，因为重写了一遍后发现问题消失了（辛苦我三天调试((((((( ）

​	另外，处理回显数据有两种方法，一种是中断，另一种是轮询，这两种都可以。

### 汇编程序

​	写一个加减乘除与或的计算器和两种模式的计时器，能够数码管显示和串口显示，随时回写接收数据。

​	在写时可能会碰到各种各样的问题，一般都是本来生效的暂停和中断突然有些指令失效了，处理了IM相关问题的话应该就是有store和load这几条指令的问题，最简单的办法是加个nop（doge）。简单，但有效，其他办法可以修复相关的问题，但是我估计要暂停。

​	整个程序在一个大循环中（感觉跟蓝桥杯电子类那个差不多。。。。）

### 总结

​	后面几天真的不好过，居然还阳了，发烧躺床上躺了几天，只能申请缓考了。。。。背单词都没怎么背。大概就这样子，计组实验就这么结束了。