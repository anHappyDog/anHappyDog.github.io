---
title: 计组P5的一些想法
date: 2022-12-3 16:43:00 +0800
author: lonelywatch
tags: [计组]
categories: [BUAA,计组]
toc: true
---

## P5

####  描述

P5是后续流水线的开始，在P4的基础上进行流水线设计，通过良好的设计，能够提高后续P的效率，减少重构的可能。

课程组要求的流水线由F,D,E,M,W 5段组成，来提高CPU的效率，由于流水线满载时会有5条指令同时运行，所以可能会有冒险，需要暂停和转发。

#### 译码方式

##### 集中式译码

在D段直接译码，控制信号流水至需要的区域。

虽然降低了后续流水级的逻辑复杂度，但是需要传输的数据很多。

##### 分布式译码

在各个阶段分别译码，不需要流水控制信号。

比较灵活，便于修改（个人觉得，因为课上有些条件写指令分布式译码修改的难度感觉更低一些）。

#### 数据通路

借用高老板的P5的图

![design](https://lonelywatch-1306651324.cos.ap-beijing.myqcloud.com/design.PNG)

#### 冒险

##### 结构冒险

结构冒险指不同指令同时需要同一资源的情况，在普林斯顿体系种，IM和DM是同一寄存器，在F和M阶段都需要使用该存储器，所以存在冒险，课程组采用哈佛体系，将IM和DM分开，故不存在数据冒险。

##### 控制冒险

控制冒险指指分支指令会影响接下来指令的执行情况（不管是否跳转，都会执行下一条指令），需要我们实现**延迟槽**。

#### 数据冒险

数据冒险指后续指令会使用前些条指令尚未写入的数据，需要暂停和转发。

#### 暂停和转发

$ T_{use}$ : 该指令再过多少个周期就需要用到数据，所以 $ T_{use} $ 可能有 $ T_{rsuse} , T_{rtuse} $  两个值，且每种指令值固定。

$ T_{new} $: 该指令再过多少个周期能产生数据流入流水线寄存器，至多有一个值，且随着流水不断变化。 

当 $ T_{use} \lt T_{new}$ 时，我们需要暂停，否则需要转发。

AT法则：根据 $ T_{use} 和 T_{new}$  构建策略矩阵，列出stall(暂停)和转发选择器的信号条件。

![atfield](https://lonelywatch-1306651324.cos.ap-beijing.myqcloud.com/atfield.PNG)

由此构造出ATCONTROL(暂停转发控制器)，

需要转发数据的有D,E,M三段，共有五处。提供转发数据的有M,W段，共有两处。同时转发有优先级，需要优先转发最新修改的数据，即M段数据。

**ATCONTRL**(现在看上去写的好笨呗):

//当时不懂沿用了老师课上的暂停转发代码，后面觉得没必要这么写，但是又懒得改了。。

```verilog
//module ATCONTROL
module ATCONTROL(
	input[1:0] trsuse,
	input[1:0] trtuse,
	input[1:0] tnewe,
	input[1:0] tnewm,
	input[1:0] tneww,
	input[4:0] rsd,
	input[4:0] rtd,
	input[4:0] rse,
	input[4:0] rte,
	input[4:0] dste,
	input[4:0] rtm,
	input[4:0] dstm,
	input[4:0] dstw,
	input regwre,
	input regwrm,
	input regwrw,
	input ismudiv,
	input busy,
	input start,
	output stall,
	output[1:0] v1todselop,
	output[1:0] v2todselop,
	output[1:0] v1toeselop,
	output[1:0] v2toeselop,
	output v1tomselop
    );
	wire stall_rs,stall_rt,stall_mudiv;
	wire stall_rs0_e2,stall_rs0_e1,stall_rs1_e2;
	wire stall_rs0_m1;
	wire stall_rt0_e2,stall_rt0_e1,stall_rt1_e2;
	wire stall_rt0_m1;
	//没必要这么写感觉。。
    assign stall_rs0_e2 = (trsuse == 2'b00) & (tnewe == 2'b10) & (rsd == dste) & (rsd != 0) & regwre;
	assign stall_rs0_e1 = (trsuse == 2'b00) & (tnewe == 2'b01) & (rsd == dste) & (rsd != 0) & regwre;
	assign stall_rs1_e2 = (trsuse == 2'b01) & (tnewe == 2'b10) & (rsd == dste) & (rsd != 0) & regwre;
	assign stall_rs0_m1 = (trsuse == 2'b00) & (tnewm == 2'b01) & (rsd == dstm) & (rsd != 0) & regwrm;
	
	assign stall_rt0_e2 = (trtuse == 2'b00) & (tnewe == 2'b10) & (rtd == dste) & (rtd != 0) & regwre;
	assign stall_rt0_e1 = (trtuse == 2'b00) & (tnewe == 2'b01) & (rtd == dste) & (rtd != 0) & regwre;
	assign stall_rt1_e2 = (trtuse == 2'b01) & (tnewe == 2'b10) & (rtd == dste) & (rtd != 0) & regwre;
	assign stall_rt0_m1 = (trtuse == 2'b00) & (tnewm == 2'b01) & (rtd == dstm) & (rtd != 0) & regwrm;
	
	assign stall_rs = stall_rs0_e2 | stall_rs0_e1 | stall_rs1_e2 | stall_rs0_m1;
	assign stall_rt = stall_rt0_e2 | stall_rt0_e1 | stall_rt1_e2 | stall_rt0_m1;
    
    //乘除法暂停
	assign stall_mudiv = ismudiv & (busy | start);
	assign stall = stall_rs | stall_rt | stall_mudiv;
	
    
    //转发，v[x]to[des]selop : x表示第几个，des表示转发目的地
	assign v1todselop = (rsd == dstm) & (tnewm == 2'b00) & (rsd != 0) & regwrm ?`TOSEL_FM:
							  (rsd == dstw) & (tneww == 2'b00) & (rsd != 0) & regwrw ?`TOSEL_FW:`TOSEL_ORI;
	assign v2todselop = (rtd == dstm) & (tnewm == 2'b00) & (rtd != 0) & regwrm ?`TOSEL_FM:
							  (rtd == dstw) & (tneww == 2'b00) & (rtd != 0) & regwrw ?`TOSEL_FW:`TOSEL_ORI;
	
	assign v1toeselop = (rse == dstm) & (tnewm == 2'b00) & (rse != 0) & regwrm ?`TOSEL_FM:
							  (rse == dstw) & (tneww == 2'b00) & (rse != 0) & regwrw ?`TOSEL_FW:`TOSEL_ORI;
	assign v2toeselop = (rte == dstm) & (tnewm == 2'b00) & (rte != 0) & regwrm ?`TOSEL_FM:
							  (rte == dstw) & (tneww == 2'b00) & (rte != 0) & regwrw ?`TOSEL_FW:`TOSEL_ORI;
							  
	assign v1tomselop = (rtm == dstw) & (tneww == 2'b00) & (rtm != 0) & regwrw ?`TOMSEL_FW:`TOMSEL_ORI;

endmodule
```



#### 延迟槽

因为分支和跳转指令无论是否跳转，下一条指令都会执行，因此可以在后面添加一条无关指令，所以，延迟槽基本不需要修改很多地方，

只需要注意jal写入31号寄存器的数据应为pcw + 8 (pcw为流水到w级的jal的pc值，因为添加了一条无关指令，所以需要从pcw + 4 变为 pcw + 8), 同时注意beq的实现即可。

注意的地方有：模块**NPC**,以及写入PC时模块**WDSEL**

```verilog
//NPC
module NPC(
		input[31:0] pc,
		input[2:0] npcop,
		input[15:0] imm16,
		input[25:0] imm26,
		input[31:0] imm32,
		input isequal,
		output[31:0] npc
    );
	 //异常和中断这里可能需要重新修改sad
	assign npc = (npcop == `NPC_PC4)? pc + 4 :
					 (npcop == `NPC_BEQ)?(isequal == 1 ? pc + {{14{imm16[15]}},imm16,2'b0}:pc + 4):
					 (npcop == `NPC_BNE)?(isequal == 0 ? pc + {{14{imm16[15]}},imm16,2'b0}:pc + 4):
					 (npcop == `NPC_JAL)?{pc[31:28],imm26,2'b0}:
					 (npcop == `NPC_JR)?imm32:pc + 4;
					 

endmodule

```

```verilog
//WDSEL
module WDSEL(
	input[31:0] aluoutw,
	input[31:0] dmoutw,
	input[31:0] pcw,
	input[1:0] wdselop,
	output[31:0] wdselout
    );
	assign wdselout = (wdselop == `WDSEL_ALU)?aluoutw:
					(wdselop == `WDSEL_DM)? dmoutw:
        			  (wdselop == `WDSEL_PC)? pcw + 8:	//pc + 8
											aluoutw;

endmodule
```



#### 测试

重点测试冲突指令，可以用魔改的Mars得到输出结果进行自动比对。

例如:

```verilog
//1
jal xxxxx	//jr beq ,bne
sw $x,x($ra)
...
//2
mudiv $x,$y
mfhi ..
mflo ..
mthi ..
mfhi ..
mtlo ..
mflo ..		//div同理

//3.
//基本是在一条指令后加上使用前一条指令的数据的指令
//
```



#### 课上指令

仔细查看RTL描述，相信很快就能解决。如果提交出现错误，最好不要尝试用数据Debug，仔细分析错误可能出现的错误可能更快一些，如果碰到条件写指令，需要根据情况来进行暂停。（我P5用的集中式译码写第一道条件写写了好久都没过现在想想应该是没有暂停1周期，还好后面的题目简单些不然就寄了>_>）

#### 后话

简单来源于规整，只有好的设计，才有利于我们在课上进行修改，所以不要吝啬时间在理解和设计上面(好想揍几个星期之前的自己)。

附上自己的代码，P6文件夹有课程组魔改的mars。

[anHappyDog/- (github.com)](https://github.com/anHappyDog/-)



